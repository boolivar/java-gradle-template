plugins {
    id "java"
    id "jacoco"
    id "pmd"
    id "checkstyle"
    id "com.github.spotbugs"
    id "io.freefair.lombok"
}

group = "org.bool"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(Javadoc).configureEach {
    options.addBooleanOption("Xdoclint:none", true)
}

tasks.named("test") {
    useJUnitPlatform()
    finalizedBy "jacocoTestReport"
}

tasks.named("jacocoTestReport") {
    reports {
        xml.required = true
    }
}

tasks.named("jacocoTestCoverageVerification") {
    dependsOn "test"
    violationRules {
        rule {
            limit {
                minimum = jacocoMinRatio as BigDecimal
            }
        }
    }
}

tasks.named("check") {
    dependsOn "jacocoTestCoverageVerification"
}

tasks.named("jar") {
    dependsOn "check"
}

checkstyle {
    configDirectory = file("$rootDir/gradle/config/checkstyle")
    toolVersion = com.puppycrawl.tools.checkstyle.Checker.package.implementationVersion
}

pmd {
    consoleOutput = true
    ruleSets = ["ruleset.xml"]
    toolVersion = net.sourceforge.pmd.PMDVersion.VERSION
}

tasks.withType(Pmd).configureEach {
    pmdClasspath += files("$rootDir/gradle/config/pmd")
}

spotbugs {
    omitVisitors = ["FindReturnRef"]
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform(project(":platform"))

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.springframework"
        exclude group: "org.springframework.boot"
    }
}